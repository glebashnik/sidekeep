//User name and avatar image could be accessed by anyone with uid (bad).
//Accessing feed content requires feed secret (good).

path /users/$userId is User {
	read() = isSignedUser();
	write() = isCurrentUser($userId);
}

path /feeds/$feed is Feed {
	read() = isFeedUser($feed);
	write() = isNew(this) || isFeedUser($feed);
}

path /users/$userId/feeds/$feedId is Boolean {
	validate() = root.feeds[$feedId].users[$userId] != null;
}

path /feeds/$feedId/users/$userId is Password {
	write() = this == root.feeds[$feedId].password;
	validate() = root.users[$feedId].feeds[$userId] != null;
}

path /posts/$feedId is Search | Page {
	read() = isFeedUser($feedId);
	write() = isFeedUser($feedId);
}

type User {
  	name: String,
  	image: String,
  	feed: String | Null
}

type Feed {
	name: String,
	password: Password
}

type Post {
	user: String,
	time: Number
}

type Search extends Post {
	url: String,
	query: String,
	pages: String[]
}

type Page extends Post {
	search: String | Null
	clips: String[],
	url: String,
	title: String,
}

type Clip extends Post {
	page: String
}

type TextClip extends Clip {
	text: String
}

type ImageClip extends Clip {
	image: String
}

type Comment extends Post {
	post: String,
	text: String
}

type Password extends String;

isSignedUser() = auth != null;

isCurrentUser(userId) = isSignedUser() && auth.uid == userId;

isFeedUser(feedId) = isSignedUser() && root.feeds[feedId].users[auth.uid] == root.feeds[feedId].password;

isNew(ref) = prior(ref) == null;